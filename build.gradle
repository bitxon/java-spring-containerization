plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.8'
    id 'io.spring.dependency-management' version '1.1.7'
    id "com.gorylenko.gradle-git-properties" version "2.5.4"
    id "org.ajoberstar.grgit" version "4.1.1"
    id 'com.google.cloud.tools.jib' version '3.5.1'
}

group = 'bitxon.containerization'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

gitProperties {
    // https://github.com/n0mer/gradle-git-properties
    keys = ['git.branch', 'git.commit.id']
}

springBoot {
    buildInfo()
}

tasks.named("bootBuildImage") {
    // https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/htmlsingle/#build-image.examples.custom-image-name
    imageName = "bitxon/${project.name}:gradle-buildpack-${grgit.head().id}"
    publish = false
    docker {
        publishRegistry {
            username = "REPLACE_WITH_REAL_USER_NAME"
            password = "REPLACE_WITH_REAL_PASSWORD"
        }
    }
}

jib {
    from {
        // Default: 'eclipse-temurin'
        image = "azul/zulu-openjdk-alpine:21.0.4-jre"
        platforms {
            platform {
                architecture = 'arm64'
                os = 'linux'
            }
        }
    }
    container {
        ports = ['8080', '8081']
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
